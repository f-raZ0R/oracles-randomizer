# don't include essence and global flag checks in here; those can go in
# asm/triggers.yaml.

seasons:
  # data for checkSetAnimalSavePoint:
  # source room, animal room, saved y, saved x.
  04/animalSavePointTable: |
      db c2,c2,18,68 # spool swamp cave
      db 29,2a,38,18 # goron mountain east cave
      db 8e,8e,58,88 # cave outside d2
      db 87,86,48,68 # cave north of d1
      db 28,2a,38,18 # goron mountain main
      db 0f,2f,18,68 # spring banana cave
      db 1f,2f,18,68 # cave below spring banana cave
      db 9a,9a,38,48 # rosa portal
      db 8d,8d,38,38 # d2 entrance
      db ff

  # if entering certain warps blocked by snow piles, mushrooms, or bushes, set
  # the animal companion to appear right outside the entrance instead of where
  # you left them.
  04/checkSetAnimalSavePoint: |
      push bc
      push de
      ld a,(wActiveRoom)
      ld b,a
      ld a,(wRememberedCompanionRoom)
      ld c,a
      ld e,02
      ld hl,animalSavePointTable
      call searchDoubleKey
      jr nc,.done
      ld de,wRememberedCompanionY
      ldi a,(hl)
      ld (de),a
      inc de
      ld a,(hl)
      ld (de),a
      .done
      pop de
      pop bc
      ld a,(wWarpDestIndex)
      ret
  04/461e/: call checkSetAnimalSavePoint

  # vanilla game doesn't save non-natzu animal positions when it thinks you
  # won't need them anymore. stop that.
  05/45c9/: jr 28

  # do this so that animals don't immediately stop walking onscreen when called
  # on a bridge, namely the one to/from d1.
  05/animalEntryIgnoreBridges: |
      call _specialObjectGetRelativeTileWithDirectionTable
      or a
      ret z
      cp a,1a
      ret z
      cp a,1b
      ret
  05/493b/: call animalEntryIgnoreBridges; nop
  05/71ea/: call animalEntryIgnoreBridges; nop

  # this is called to make moosh unrideable on mt cucco in the case of not
  # having flute in a moosh seed.
  05/checkFlute: |
      ld a,TREASURE_FLUTE
      jp checkTreasureObtained
  05/776b/: call checkFlute
  05/7a65/: call checkFlute

  # animals called by flute normally veto any nonzero collision value for the
  # purposes of entering a screen, but this allows double-wide bridges (1a and
  # 1b) as well. this specifically fixes the problem of not being able to call
  # an animal on the d1 screen, or on the bridge to the screen to the right.
  # the vertical collision check isn't modified, since bridges only run
  # horizontally.
  09/checkFluteCollisions: |
      ld b,01
      ld a,(hl)
      cp a,1a
      jr z,.firstTileMatched
      cp a,1b
      jr z,.firstTileMatched
      or a
      ret nz
      .firstTileMatched
      ld a,l
      add a,b
      ld l,a
      ld a,(hl)
      cp a,1a
      jr z,.secondTileMatched
      cp a,1b
      jr z,.secondTileMatched
      or a
      .secondTileMatched
      ld a,l
      ret nz
      call convertShortToLongPosition
      xor a
      ret
  09/4d9a/: call checkFluteCollisions
  09/4dad/: call checkFluteCollisions

  # some of the ricky code here doesn't matter since ricky's flute isn't
  # currently randomized (6cefde1), but it can stay in case things change.

  # check flute icon instead of animal region for dimitri events:
  09/4e4b/: ld a,(c6af); cp a,02 # spawning dimitri + kids in sunken
  09/6f07/: ld a,(c6af); cp a,02 # ^
  09/737d/: ld a,(c6af); cp a,02 # ^
  09/6f34/: ld a,(c6af); cp a,02 # trying to leave sunken w/ dimitri

  # check flute icon instead of animal region for ricky events:
  09/4e76/: ld a,(wFluteIcon); cp a,01 # spawn ricky in pen
  09/6ccc/: ld a,(wFluteIcon); cp a,01 # say goodbye when reaching spool

  # prevent subrosian dancing from giving dimitri's flute.
  09/5e37/: or a,20

  # stop ricky from giving his flute.
  09/6e6c/: ret
  0b/6b77/: db jumpifmemoryeq; dw wAnimalRegion; db 7f

  # prevent holodrum plain from changing the animal region when entered.
  09/6f79/: jr 08

  # don't set a ricky flag when buying 150-rupee shop item.
  0b/4823/: db ormemory; dw wRickyState; db 00

  # remove the moosh and dimitri events in spool swamp.
  11/6572/: db ff
  11/68d4/: db ff

  # add proper treasure entries for each flute, like ages has.
  15/fluteTreasureData: |
      db 0a,0b,38,3c
      db 0a,0c,39,3d
      db 0a,0d,3a,3e
  15/5161/: db 80; dw fluteTreasureData; db 00

ages:
  # allow calling animals on the west coast of crescent island.
  # this sets additional bits in a field indexed by room.
  0a/4c78/: db 00,03,00,02,00,02,00,02

  # allow exiting moosh/ghost screen without killing the ghosts.
  0a/595a/: nop; nop; nop

  # don't delete companion when picking up cheval's invention.
  0c/7234/: db ormemory; dw wRememberedCompanionId; db 00
  3f/6d00/: db 00

  # remove flute from lynna shooting gallery prizes.
  15/51d8/: db jumpifitemobtained,TREASURE_PUNCH

  # vanilla bug(?): moosh appears on the screen south of cheval's grave after
  # visiting the cheval's grave screen, whether you've obtained him or not.
  12/5c5d/: db ff
